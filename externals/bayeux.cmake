# Have to vendor Bayeux+CAMP dep temporarily...
include(externals/extcommon.cmake)

set(Bayeux_LIBRARY ${EXTERNALS_INSTALL_LIBDIR}/${CMAKE_SHARED_LIBRARY_PREFIX}Bayeux${CMAKE_SHARED_LIBRARY_SUFFIX})

set(Bayeux_INCLUDE_DIRS ${EXTERNALS_INSTALL_INCLUDEDIR} ${EXTERNALS_INSTALL_INCLUDEDIR}/bayeux)

foreach(_dir ${Bayeux_INCLUDE_DIRS})
  if(NOT EXISTS ${_dir})
    message(STATUS "making directory ${_dir}")
    file(MAKE_DIRECTORY ${_dir})
  endif()
endforeach()


ExternalProject_Add(CAMP_EP
  GIT_REPOSITORY https://github.com/drbenmorgan/camp.git
  GIT_TAG 7564e57f7b406d1021290cf2260334d57d8df255
  GIT_SHALLOW TRUE
  UPDATE_COMMAND ""
  INSTALL_DIR ${EXTERNALS_INSTALL_PREFIX}
  CMAKE_CACHE_ARGS
    -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
    -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
    -DCMAKE_CXX_STANDARD:STRING=17
    -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
    -DCMAKE_CXX_FLAGS_RELEASE:STRING=${CMAKE_CXX_FLAGS_RELEASE}
    -DCMAKE_CXX_FLAGS_DEBUG:STRING=${CMAKE_CXX_FLAGS_DEBUG}
    -DCMAKE_CXX_FLAGS_OPT:STRING=${CMAKE_CXX_FLAGS_OPT}
    -DCMAKE_CXX_FLAGS_PROF:STRING=${CMAKE_CXX_FLAGS_PROF}
  )

ExternalProject_Add(BAYEUX_EP
  GIT_REPOSITORY https://github.com/BxCPPDev/Bayeux.git
  GIT_TAG f3807c6dc8e921246be51d34531e4074dc635418
  GIT_SHALLOW TRUE
  UPDATE_COMMAND ""
  PATCH_COMMAND patch -p1 < ${CMAKE_CURRENT_LIST_DIR}/bayeux-pr-41.patch
  INSTALL_DIR ${EXTERNALS_INSTALL_PREFIX}
  CMAKE_CACHE_ARGS
    -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
    -DCMAKE_INSTALL_LIBDIR:STRING=lib
    -DCMAKE_PREFIX_PATH:PATH=<INSTALL_DIR>:${CMAKE_PREFIX_PATH}
    -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
    -DBAYEUX_CXX_STANDARD:STRING=17
    -DBAYEUX_COMPILER_ERROR_ON_WARNING:BOOL=OFF
    -DBAYEUX_ENABLE_TESTING:BOOL=ON
    -DBAYEUX_WITH_DEVELOPER_TOOLS:BOOL=OFF
    -DBAYEUX_WITH_DOCS:BOOL=OFF
    -DBAYEUX_WITH_EMFIELD:BOOL=OFF
    -DBAYEUX_WITH_GEANT4_MODULE:BOOL=OFF
    -DBAYEUX_WITH_QT_GUI:BOOL=OFF
  TEST_COMMAND ${CMAKE_CTEST_COMMAND} ${PARALLEL_ARG} -E ihs
  TEST_BEFORE_INSTALL TRUE
  DEPENDS CAMP_EP
  BUILD_BYPRODUCTS
    ${Bayeux_LIBRARY}
    ${Bayeux_INCLUDE_DIRS}
  )

# - Create "imported" target for Bayeux so we can link to it just as if
#   we'd called find_package(Bayeux).
add_library(Bayeux::Bayeux UNKNOWN IMPORTED)
set_property(TARGET Bayeux::Bayeux PROPERTY IMPORTED_LOCATION ${Bayeux_LIBRARY})
set_property(TARGET Bayeux::Bayeux PROPERTY INTERFACE_INCLUDE_DIRECTORIES "${Bayeux_INCLUDE_DIRS}")
set_property(TARGET Bayeux::Bayeux PROPERTY INTERFACE_LINK_LIBRARIES "${Boost_LIBRARIES}")

add_dependencies(Bayeux::Bayeux BAYEUX_EP)
